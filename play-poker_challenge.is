### {
	name: "ãƒãƒ¼ã‚«ãƒ¼ãƒãƒ£ãƒ¬ãƒ³ã‚¸"
	author: "zawa-ch."
	version: "1.0.4"
	description: "ãƒ†ã‚­ã‚¹ãƒˆã‚’å…¥åŠ›ã—ã¦ãƒ‰ãƒ­ãƒ¼ï¼åŒã˜seedã§ã‚‚å†…å®¹ã¯æ—¥æ›¿ã‚ã‚Šã§ã™"
	license: {
		name: "MIT License"
		href: "http://www.opensource.org/licenses/mit-license.php"
	}
}

@Poker() {
	let SUITS = {
		spade: { rank: 0, name: 'spade', symbol: 'â™ ', color: null },
		heart: { rank: 1, name: 'heart', symbol: 'â™¥', color: 'e11' },
		diamond: { rank: 2, name: 'diamond', symbol: 'â™¦', color: 'e11' },
		club: { rank: 3, name: 'club', symbol: 'â™£', color: null },
	}
	let NUMBERS = {
		ace: { rank: 0, name: 'ace', symbol: 'A' }
		king: { rank: 1, name: 'king', symbol: 'K' }
		queen: { rank: 2, name: 'queen', symbol: 'Q' }
		jack: { rank: 3, name: 'jack', symbol: 'J' }
		ten: { rank: 4, name: 'ten', symbol: '10' }
		nine: { rank: 5, name: 'nine', symbol: '9' }
		eight: { rank: 6, name: 'eight', symbol: '8' }
		seven: { rank: 7, name: 'seven', symbol: '7' }
		six: { rank: 8, name: 'six', symbol: '6' }
		five: { rank: 9, name: 'five', symbol: '5' }
		four: { rank: 10, name: 'four', symbol: '4' }
		three: { rank: 11, name: 'three', symbol: '3' }
		two: { rank: 12, name: 'two', symbol: '2' }
	}
	@Card(suits, number, card) {
		@to_mfm() {
			[
				if Core:type(suits.color) == 'str' { `$[fg.color={suits.color} ` } else { '' }
				card
				if Core:type(suits.color) == 'str' { ']' } else { '' }
			].join()
		}
		@to_string() {
			`{number.name} of {suits.name}`
		}
		@to_symbol() {
			`{suits.symbol}{number.symbol}`
		}
		{ suits: suits, number: number, card: card, to_mfm: to_mfm, to_string: to_string, to_symbol: to_symbol }
	}
	@card_comp(l, r) { if l.number.rank == r.number.rank { if l.suits.rank == r.suits.rank { 0 } elif l.suits.rank > r.suits.rank { 1 } else { -1 } } elif l.number.rank > r.number.rank { 1 } else { -1 } }
	let DECK_BASE = [
		Card(SUITS.spade, NUMBERS.ace, 'ðŸ‚¡'), Card(SUITS.heart, NUMBERS.ace, 'ðŸ‚±'), Card(SUITS.diamond, NUMBERS.ace, 'ðŸƒ'), Card(SUITS.club, NUMBERS.ace, 'ðŸƒ‘'),
		Card(SUITS.spade, NUMBERS.king, 'ðŸ‚®'), Card(SUITS.heart, NUMBERS.king, 'ðŸ‚¾'), Card(SUITS.diamond, NUMBERS.king, 'ðŸƒŽ'), Card(SUITS.club, NUMBERS.king, 'ðŸƒž'),
		Card(SUITS.spade, NUMBERS.queen, 'ðŸ‚­'), Card(SUITS.heart, NUMBERS.queen, 'ðŸ‚½'), Card(SUITS.diamond, NUMBERS.queen, 'ðŸƒ'), Card(SUITS.club, NUMBERS.queen, 'ðŸƒ'),
		Card(SUITS.spade, NUMBERS.jack, 'ðŸ‚«'), Card(SUITS.heart, NUMBERS.jack, 'ðŸ‚»'), Card(SUITS.diamond, NUMBERS.jack, 'ðŸƒ‹'), Card(SUITS.club, NUMBERS.jack, 'ðŸƒ›'),
		Card(SUITS.spade, NUMBERS.ten, 'ðŸ‚ª'), Card(SUITS.heart, NUMBERS.ten, 'ðŸ‚º'), Card(SUITS.diamond, NUMBERS.ten, 'ðŸƒŠ'), Card(SUITS.club, NUMBERS.ten, 'ðŸƒš'),
		Card(SUITS.spade, NUMBERS.nine, 'ðŸ‚©'), Card(SUITS.heart, NUMBERS.nine, 'ðŸ‚¹'), Card(SUITS.diamond, NUMBERS.nine, 'ðŸƒ‰'), Card(SUITS.club, NUMBERS.nine, 'ðŸƒ™'),
		Card(SUITS.spade, NUMBERS.eight, 'ðŸ‚¨'), Card(SUITS.heart, NUMBERS.eight, 'ðŸ‚¸'), Card(SUITS.diamond, NUMBERS.eight, 'ðŸƒˆ'), Card(SUITS.club, NUMBERS.eight, 'ðŸƒ˜'),
		Card(SUITS.spade, NUMBERS.seven, 'ðŸ‚§'), Card(SUITS.heart, NUMBERS.seven, 'ðŸ‚·'), Card(SUITS.diamond, NUMBERS.seven, 'ðŸƒ‡'), Card(SUITS.club, NUMBERS.seven, 'ðŸƒ—'),
		Card(SUITS.spade, NUMBERS.six, 'ðŸ‚¦'), Card(SUITS.heart, NUMBERS.six, 'ðŸ‚¶'), Card(SUITS.diamond, NUMBERS.six, 'ðŸƒ†'), Card(SUITS.club, NUMBERS.six, 'ðŸƒ–'),
		Card(SUITS.spade, NUMBERS.five, 'ðŸ‚¥'), Card(SUITS.heart, NUMBERS.five, 'ðŸ‚µ'), Card(SUITS.diamond, NUMBERS.five, 'ðŸƒ…'), Card(SUITS.club, NUMBERS.five, 'ðŸƒ•'),
		Card(SUITS.spade, NUMBERS.four, 'ðŸ‚¤'), Card(SUITS.heart, NUMBERS.four, 'ðŸ‚´'), Card(SUITS.diamond, NUMBERS.four, 'ðŸƒ„'), Card(SUITS.club, NUMBERS.four, 'ðŸƒ”'),
		Card(SUITS.spade, NUMBERS.three, 'ðŸ‚£'), Card(SUITS.heart, NUMBERS.three, 'ðŸ‚³'), Card(SUITS.diamond, NUMBERS.three, 'ðŸƒƒ'), Card(SUITS.club, NUMBERS.three, 'ðŸƒ“'),
		Card(SUITS.spade, NUMBERS.two, 'ðŸ‚¢'), Card(SUITS.heart, NUMBERS.two, 'ðŸ‚²'), Card(SUITS.diamond, NUMBERS.two, 'ðŸƒ‚'), Card(SUITS.club, NUMBERS.two, 'ðŸƒ’'),
	]
	@get_rank_weakace(card) { if card.number.rank == 0 { 13 } else { card.number.rank } }
	@card_comp_weakace(l, r) { if get_rank_weakace(l) == get_rank_weakace(r) { if l.suits.rank == r.suits.rank { 0 } elif l.suits.rank > r.suits.rank { 1 } else { -1 } } elif get_rank_weakace(l) > get_rank_weakace(r) { 1 } else { -1 } }
	@is_flush(cardarr) { cardarr.reduce(@(a,i,n){ if Core:type(a) != 'null' { { suits: a.suits, result: a.result && ( a.suits.rank == i.suits.rank ) } } else { { suits: i.suits, result: true } } }, null).result }
	@is_straight(cardarr) { cardarr.copy().sort(card_comp).reduce(@(a,i,n){ if Core:type(a) != 'null' { { rank: i.number.rank, result: a.result && ( (a.rank + 1) == i.number.rank ) } } else { { rank: i.number.rank, result: true } } }, null).result }
	@is_straight_weakace(cardarr) { cardarr.copy().sort(card_comp_weakace).reduce(@(a,i,n){ if Core:type(a) != 'null' { { rank: get_rank_weakace(i), result: a.result && ( (a.rank + 1) == get_rank_weakace(i) ) } } else { { rank: get_rank_weakace(i), result: true } } }, null).result }
	@is_royalstraight(cardarr) { cardarr.copy().sort(card_comp).reduce(@(a,i,n){ if Core:type(a) != 'null' { { rank: a.rank + 1, result: a.result && ( (a.rank + 1) == i.number.rank ) } } else { { rank: 0, result: i.number.rank == 0 } } }, null).result }
	@aggregate_pairs(cardarr) {
		cardarr.reduce(@(a,i,n){
			let f = a.find(@(ai,an){ ai.number.rank == i.number.rank })
			if Core:type(f) != 'null' { f.cards.push(i) } else { a.push({ number: i.number, cards: [i] }) }
			a
		}, [])
	}
	@sort_with_pairs(cardarr) {
		aggregate_pairs(cardarr.sort(card_comp)).sort(@(l,r){ if l.cards.len == r.cards.len { l.number.rank - r.number.rank } else { r.cards.len - l.cards.len } }).reduce(@(a,i,n){ a.concat(i.cards) }, [])
	}
	@sort_with_straight(cardarr) {
		if is_straight_weakace(cardarr) { cardarr.sort(card_comp_weakace) } else { cardarr.sort(card_comp) }
	}
	@HAND_ROLES() { [
		{ rank: 0, name: 'royal straight flush', check: @(cardarr) { is_flush(cardarr) && is_royalstraight(cardarr) }, sort: @(cardarr) { cardarr.sort(card_comp) } }
		{ rank: 1, name: 'straight flush', check: @(cardarr) { is_flush(cardarr) && ( is_straight(cardarr) || is_straight_weakace(cardarr) ) }, sort: @(cardarr) { sort_with_straight(cardarr) } }
		{ rank: 2, name: 'four of a kind', check: @(cardarr) { aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len >= 4 }).len >= 1 }, sort: @(cardarr) { sort_with_pairs(cardarr) } }
		{ rank: 3, name: 'full house', check: @(cardarr) { (aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len == 3 }).len >= 1) && (aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len == 2 }).len >= 1) }, sort: @(cardarr) { sort_with_pairs(cardarr) } }
		{ rank: 4, name: 'flush', check: @(cardarr) { is_flush(cardarr) }, sort: @(cardarr) { cardarr.sort(card_comp) } }
		{ rank: 5, name: 'straight', check: @(cardarr) { is_straight(cardarr) || is_straight_weakace(cardarr) }, sort: @(cardarr) { sort_with_straight(cardarr) } }
		{ rank: 6, name: 'three of a kind', check: @(cardarr) { aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len >= 3 }).len >= 1 }, sort: @(cardarr) { sort_with_pairs(cardarr) } }
		{ rank: 7, name: 'two pair', check: @(cardarr) { aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len >= 2 }).len >= 2 }, sort: @(cardarr) { sort_with_pairs(cardarr) } }
		{ rank: 8, name: 'one pair', check: @(cardarr) { aggregate_pairs(cardarr).filter(@(i,n){ i.cards.len >= 2 }).len >= 1 }, sort: @(cardarr) { sort_with_pairs(cardarr) } }
		{ rank: 9, name: 'high card', check: @(cardarr) { true }, sort: @(cardarr) { cardarr.sort(card_comp) } }
	] }
	@CardBundle(cardarr) {
		var data = if Core:type(cardarr) == 'arr' { cardarr.copy() } else { [] }

		@add(card) {
			data.push(card)
		}
		@remove(index) {
			let r = data[index]
			data = data.filter(@(i,n){ n != index })
			r
		}
		@sort() {
			data = data.sort(card_comp)
		}
		{ cards: @() { data }, count: @(){ data.len }, add: add, remove: remove, sort: sort }
	}
	@Hands(table) {
		var hand = CardBundle(null)
		for 5 { hand.add(table.pick_card()) }
		@to_role() {
			var r = HAND_ROLES()
			r = r.filter(@(i,n){ i.check(hand.cards()) })
			if r.len > 0 { r[0] } else { null }
		}
		@sort() {
			var roles = to_role()
			if Core:type(roles) != 'null' { hand = CardBundle(roles.sort(hand.cards())) } else { hand.sort() }
		}
		@to_string() {
			hand.cards().map(@(i,n){ i.card }).join()
		}
		@to_mfm() {
			hand.cards().map(@(i,n){ i.to_mfm() }).join()
		}
		{ cards: @(){ hand }, to_role: to_role, sort: sort, to_string: to_string, to_mfm: to_mfm }
	}
	@Table(seed) {
		var deck = CardBundle(DECK_BASE)
		let rand = Math:gen_rng(seed)
		@pick_card() {
			let i = rand(0, deck.count() - 1)
			deck.remove(i)
		}
		{ deck: @(){ deck }, pick_card: pick_card }
	}

	{ create_table: @(seed) { Table(seed) }, create_hand: @(table) { Hands(table) } }
}

let HASHTAG = '#ãƒãƒ¼ã‚«ãƒ¼ãƒãƒ£ãƒ¬ãƒ³ã‚¸'

let seed = ''
let seed_v = ''
let seedinput = Ui:C:textInput({
	onInput: @(text) {
		seed = text
	}
	default: ''
	label: 'seed'
	caption: ''
}, 'seedinput')
let seedfolder = Ui:C:folder({
	children: [ seedinput ]
	title: 'seed'
	opened: false
}, 'seedfolder')
let drawbutton = Ui:C:button({
	text: 'Draw!'
	onClick: @(){
		drawbutton.update({ disabled: true })
		seed_v = if seed.len > 0 { seed } else { Util:uuid() }
		var hand = game.create_hand(game.create_table(`{Date:year()}:{Date:month()}:{Date:day()}:{seed_v}`))
		hand.sort()
		let result = [
			`<center>$[x3 {hand.to_mfm()}]`
			`$[font.serif {hand.to_role().name.upper()}]</center>`
			''
			`{THIS_URL} {HASHTAG}`
		].join(Str:lf)
		seedtext.update({text: `seed: {seed_v}`})
		resulttext.update({ text: result })
		postformbutton.update({ form: { text: result } })
		resultarea.update({ hidden: false })
		drawbutton.update({ disabled: false })
	}
	primary: false
	disabled: false
}, 'drawbutton')
let preparearea = Ui:C:container({
	children: [ drawbutton, seedfolder ]
}, 'preparearea')
let seedtext = Ui:C:text({ text: '', size: 0.75 }, 'seedtext')
let resulttext = Ui:C:mfm({ text: '' }, 'resulttext')
let postformbutton = Ui:C:postFormButton({
	text: "Note!"
	primary: true
	form: { text: '' }
}, 'postformbutton')
let resultarea = Ui:C:container({
	children: [ Ui:C:text({ text: 'Result', size: 1.2 }), seedtext, resulttext, postformbutton ]
	hidden: true
}, 'resultarea')

let game = Poker()

@on_load() {
	Ui:render([ preparearea, resultarea ])
}
on_load()
